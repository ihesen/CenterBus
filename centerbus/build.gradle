apply plugin: 'com.android.library'
//--------------------------- 上传到Jcenter -------------------------------
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'
version = "1.0.0"
//--------------------------- 上传到Jcenter -------------------------------

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.1"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:23.+'
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
}

//--------------------------- 上传到Jcenter -------------------------------
def siteUrl = 'https://github.com/ihesen/CenterBus' // 项目的主页 这个是说明，可随便填
def gitUrl = 'https://github.com/ihesen/CenterBus' // Git仓库的url 这个是说明，可随便填
group = "com.ichsy.centerbus" // 这里是groupId ,必须填写 一般填你唯一的包名，对应com.squareup.okhttp3:okhttp:3.4.1中的com.squareup.okhttp3部分

install {
    repositories.mavenInstaller {
// This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
// Add your description here
                name 'CenterBus' //项目描述
                url siteUrl
// Set your license
                licenses {
                    license {
                        name 'The CenterBus Software License, Version 1.0.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'ihesen' //填写开发者的一些基本信息
                        name 'ihesen' //填写开发者的一些基本信息
                        email 'ihesen@163.com' //填写开发者的一些基本信息
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    options {
        encoding "UTF-8"
        charSet 'UTF-8'
        author true
        version true
        links "http://docs.oracle.com/javase/7/docs/api"
    }
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
//local.properties这个文件配置Bintray账户好密钥（注意local.properties文件的位置）
properties.load(project.file('../local.properties').newDataInputStream())
bintray {
// userOrg=properties.getProperty("bintray.user")
    user = properties.getProperty("bintray.user") //读取 local.properties 文件里面的 bintray.user
    key = properties.getProperty("bintray.apikey") //读取 local.properties 文件里面的 bintray.apikey
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "CenterBus" //发布到JCenter上的项目名字，必须填写，对应com.squareup.okhttp3:okhttp:3.4.1中的okhttp
//        publishVersion = "1.0.0"//发布到JCenter上的项目版本号（貌似不注释同步会有问题）
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}
//--------------------------- 上传到Jcenter -------------------------------


